apply plugin: 'idea'
apply plugin: 'com.android.library'

android {
    def versionMajor = 1
    def versionMinor = 2
    def versionPatch = 1

    compileSdkVersion global_compileSdkVersion
    buildToolsVersion global_buildToolsVersion
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 26
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    // Rename the aar correctly
    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "microconnect-${variant.baseName}-${versionMajor}.${versionMinor}.${versionPatch}.aar"
                outputFile = new File(outputFile.parent, fileName)
            }
            return outputFile
        }
    }
}

android.packagingOptions {
    exclude 'META-INF/services/com.fasterxml.jackson.databind.Module'
//    exclude 'META-INF/NOTICE' // will not include NOTICE file
//    exclude 'META-INF/LICENSE' // will not include LICENSE file
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

configurations {
    all*.exclude group: 'xpp3', module: 'xpp3'
}

dependencies {
    compile "com.android.support:support-v4:$global_supportLibVersion"
    compile("com.android.support:appcompat-v7:$global_supportLibVersion") {
        exclude module: 'support-v4'
    }
    compile "com.squareup.okhttp3:okhttp:3.2.0"
    compile "org.igniterealtime.smack:smack-android-extensions:$global_smackLibVersion"
    compile "org.igniterealtime.smack:smack-tcp:$global_smackLibVersion"
    compile "joda-time:joda-time:$global_jodatimeVersion"

    androidTestCompile("com.android.support.test.espresso:espresso-core:$global_espressoVersion", {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile "junit:junit:$global_junitVersion"
}

